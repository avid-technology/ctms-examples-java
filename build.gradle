apply from: 'versions.gradle'

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
}

subprojects {
    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "org.jboss.resteasy:resteasy-client:${rootProject.resteasyVersion}"
        implementation "com.konghq:unirest-java:${rootProject.unirestVersion}"
        implementation "io.projectreactor:reactor-core:${rootProject.reactorVersion}"
        implementation "com.damnhandy:handy-uri-templates:${rootProject.handyURITemplatesVersion}"
        implementation "com.fasterxml.jackson.core:jackson-databind:${rootProject.jacksonCoreVersion}"
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
}

// ================================= TASKS

// :)
final destination = 'dest'

task copyJars(type: Copy, dependsOn: subprojects.jar) {
    from subprojects.jar
    into project.file(destination)
}

task copyLibs(type: Copy) {
    from 'lib'
    into project.file("$destination/lib")
}

task copyDeps(type: Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from subprojects.configurations.runtimeClasspath
    into project.file("$destination/lib")
}

task copyArgumentFiles(type: Copy) {
    from 'resources'
    into project.file("$destination/resources")
}

task copyReadmeFiles(type: Copy) {
    from 'Readme.md'
    into project.file(destination)
}

task clearArtifacts(type: Delete) {
    delete destination
}

clean {
    dependsOn clearArtifacts
}

build {
    dependsOn copyLibs
    dependsOn copyJars
    dependsOn copyDeps
    dependsOn copyReadmeFiles
    dependsOn copyArgumentFiles
}
